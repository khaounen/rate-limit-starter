import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.30.0'
}

group = 'com.khaounen'
version = (findProperty('releaseVersion') ?: System.getenv('RELEASE_VERSION') ?: '0.1.1-SNAPSHOT')
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:3.1.5")
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'org.springframework:spring-web'
    api 'org.springframework.security:spring-security-core'
    api 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    compileOnly 'jakarta.servlet:jakarta.servlet-api'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.springframework.boot:spring-boot-starter-mail'
    compileOnly 'org.springframework.data:spring-data-redis'
    compileOnly 'org.projectlombok:lombok:1.18.20'

    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:3.1.5")
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    pom {
        name = 'rate-limit-starter'
        description = 'Rate limiting starter with fingerprint support.'
        url = 'https://github.com/khaounen/rate-limit-starter'
        licenses {
            license {
                name = 'Apache-2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0'
            }
        }
        developers {
            developer {
                id = 'khaledawnen'
                name = 'Khaled Awnen'
            }
        }
        scm {
            url = 'https://github.com/khaounen/rate-limit-starter'
            connection = 'scm:git:git://github.com/khaounen/rate-limit-starter.git'
            developerConnection = 'scm:git:ssh://github.com/khaounen/rate-limit-starter.git'
        }
    }
}
